import org.ajoberstar.grgit.Grgit

apply plugin: 'com.android.application'


ext {
    git = Grgit.open()
    revision = git.head().id
    ext.versionMajor = 0
    ext.versionMinor = 0
    ext.versionPatch = git.log(includes: ['HEAD']).size()
    supportLibVersion = '26.0.1'
    playServicesVersion = '11.0.4'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release_config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.anontemp.android"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode computeVersionCode()
        versionName computeVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_config
        }
        debug {
            debuggable true
        }
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    lintOptions {
        disable 'RtlSymmetry', 'ContentDescription', 'LabelFor', 'InefficientWeight'
        abortOnError false
    }
}

def computeVersionName() {
    return String.format('%d.%d.%d (%s)', versionMajor, versionMinor, 0, git.head().abbreviatedId)
}

def computeVersionCode() {
    return (versionMajor * 100000) + (versionMinor * 10000) + versionPatch
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com'
        }
        maven { url 'https://maven.fabric.io/public' }

    }

    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
        classpath 'io.fabric.tools:gradle:1.+'


    }
}
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"

    compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
    compile "com.google.firebase:firebase-database:$playServicesVersion"
    compile "com.google.firebase:firebase-storage:$playServicesVersion"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.scottyab:secure-preferences-lib:0.1.4'
    compile 'org.apache.commons:commons-text:1.1'
    compile 'com.github.nekocode:Emojix:1.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.firebaseui:firebase-ui-storage:2.2.0'
    testCompile 'junit:junit:4.12'
}











apply plugin: 'com.google.gms.google-services'